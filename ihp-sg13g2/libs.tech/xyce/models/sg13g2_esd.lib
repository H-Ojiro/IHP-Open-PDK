********************************************************************************************************
*
*
*
*                        COMMON ESD MODELS
*
*
*
***************************************************************************************************
*
*
*.MODEL diodevdd_2kv_mod diode (tnom=27 level=1 is=4.911e-019 rs=289.6 n=1.04 isr=5.422e-015 ikf=10m cj=8.716e-016 cjsw=4.068e-017 mj=0.3294 mjsw=0.118 vj=0.6621 vjsw=0.15 bv=11.81 ibv=4.744e-016 ibvl=1.067e-012 nbv=1 nbvl=35.32 nz=1.046 tlev=1 eg=1.11 xti=4 tbv1=-0.0008212 gleak=4.45e-015 cminr=0.01)          

********************************************************


*************************************
*

.MODEL diodevdd_mod D (tnom = 27 level = 1 is=4.911e-019 rs=289.6 n=1.04 isr = 5.422E-015 ikf=10m cj0=8.716e-016 m=0.3294 vj=0.6621  bv=11.81 ibv=4.744e-016 ibvl=1.067e-012 nbv=1 nbvl=35.32  eg=1.11 xti=4 tbv1=-0.0008212 )          
.MODEL diodevss_mod D (tnom = 27 level = 1 is=9.017E-019 rs=200   n=1.03 isr=3.776E-015   ikf=0.0001754 cj0=9.42E-016  m=0.3012  vj=0.6684 bv=11.28 ibv=1E-009 ibvl=1E-018 nbv=1.324 nbvl=1  eg=1.17 xti=3 tbv1=-0.000724 )
.MODEL dsub_2kv_mod D (tnom = 27 is = 1.7E-016 rs = 4.759 n = 1  ikf = 10m  cj = 99.77E-018   vj = 0.4547 bv = 10 ibv = 1E-019  xti = 3 tbv1 = 0 eg = 1.11 )
.MODEL dsub_4kv_mod D (tnom = 27 is = 1.7E-016 rs = 4.759 n = 1  ikf = 100u cj = 99.77E-018   vj = 0.4547 bv = 10 ibv = 0       xti = 3 tbv1 = 0 eg = 1.11 )

.include sg13g2_nmoscl_parm.lib

********************************************************


 
.subckt diodevdd_2kv VDD PAD VSS
.param DEV_A=35 DEV_P=58.08 sub_A=224.9559 sub_P=81.12 m=1
diodevdd_2kv PAD VDD diodevdd_mod area =DEV_A  m=m 
dsub_2kv VSS VDD dsub_2kv_mod area=sub_A  m=m
.ends diodevdd_2kv 
 
*************************************

.subckt idiodevdd_2kv VDD PAD VSS
.param DEV_A=35  DEV_P=58.08  sub_A=224.9559  sub_P=81.12 m=1
diodevdd_2kv PAD  VDD diodevdd_mod area = DEV_A  m=m 
dsub_2kv VSS  VDD dsub_2kv_mod area = sub_A   m=m
.ends idiodevdd_2kv
**************************************

.subckt diodevdd_4kv VDD PAD VSS
.param DEV_A=70  DEV_P=116.2  sub_A=381.0339  sub_P=90.32 m= 1

diodevdd_4kv PAD  VDD diodevdd_mod area = DEV_A  m=m 
dsub_4kv VSS  VDD dsub_4kv_mod area = sub_A  m=m 
.ends diodevdd_4kv_mod

*******************************************************
.subckt idiodevdd_4kv VDD PAD VSS
.param DEV_A=70  DEV_P=116.2  sub_A=381.0339  sub_P=90.32 m=1

diodevdd_4kv PAD  VDD diodevdd_mod area = DEV_A  m=m 
dsub_4kv VSS  VDD dsub_4kv_mod area = sub_A  m=m 
.ends idiodevdd_4kv_mod
************************************************************

.subckt diodevss_2kv VDD PAD VSS
.param DEV_A=35  DEV_P=58.08  sub_A=224.9559  sub_P=81.12 m=1

diodevss_2kv VSS PAD diodevss_mod area = DEV_A  m=m          
dsub_2kv VSS VDD dsub_2kv_mod area = sub_A  m=m
.ends diodevss_2kv
***********************************************************

.subckt idiodevss_2kv VDD PAD VSS
.param DEV_A=35  DEV_P=58.08  sub_A=224.9559  sub_P=81.12 m=1
diodevss_2kv VSS PAD diodevss_mod area = DEV_A  m=m
dsub_2kv  VSS VDD dsub_2kv_mod area=sub_A  m=m 
.ends idiodevss_2kv
************************************************************

.subckt diodevss_4kv VDD PAD VSS
.param DEV_A=70  DEV_P=116.2  sub_A=381.0339  sub_P=90.32 m=1

diodevss_4kv VSS PAD diodevss_mod area = DEV_A  m=m 
dsub_4kv VSS  VDD dsub_4kv_mod area = sub_A  m=m 
.ends diodevss_4kv
***************************************************************

.subckt idiodevss_4kv VDD PAD VSS 
.param DEV_A=70  DEV_P=116.2  sub_A=381.0339  sub_P=90.32 m=1
diodevss_4kv VSS  PAD diodevss_mod area = DEV_A  m=m 
dsub_4kv VSS VDD dsub_4kv_mod area = sub_A  m=m 
.ends idiodevss_4kv
****************************************************************

****************************************************************
.subckt nmoscl_2  VDD VSS
.param l=0.36u w=168u m=1
.param sg13g2_lv_nmos_vfbo_mm= 1.0
.param sg13g2_lv_nmos_ctl    = 1.2080
.param sg13g2_lv_nmos_rsw1   = 0.7200
.param sg13g2_lv_nmos_muew   = 0.8500
.param sg13g2_lv_nmos_dphibo = 0.9915
.param sg13g2_lv_nmos_dphibl = 0.9693
.param sg13g2_lv_nmos_dphibw = 0.9749
.param sg13g2_lv_nmos_dphiblw= 0.9754
.param sg13g2_lv_nmos_themuo = 0.8757
.param sg13g2_lv_nmos_thesatl= 0.7850
.param sg13g2_lv_nmos_thesatw= 1.5000
.param sg13g2_lv_nmos_thesatlw= 0.6127
.param sg13g2_lv_nmos_toxo   = 1.0000
.param sg13g2_lv_nmos_toxovo = 1.0000
.param sg13g2_lv_nmos_cjorbot= 1.0000
.param sg13g2_lv_nmos_cjorsti= 1.0000
.param sg13g2_lv_nmos_cjorgat= 1.0000

nmoscl_2 VDD VSS VSS VSS nmoscl_2_psp w = w  l = l m=m as = 58e-12  ad = 58e-12 pd = 336u ps = 336u
.ends nmoscl_2

*****************************************************************

.subckt nmoscl_4  VDD VSS
.param l=0.36u w=168u m=1
nmoscl_4 VDD VSS VSS VSS nmoscl_4_psp w = w  l = l m=m as = 114e-12  ad = 114e-12 pd = 672u ps = 672u
.ends nmoscl_4

